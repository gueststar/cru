#!@SED@ -nf

# Create the remaining html front matter and footers after everything
# else has been done.

# print the header

/\.TH/i\
<!DOCTYPE html>\
<html lang="en" style="background: @BACKGROUND_COLOR@;">\
<head>\
<meta charset="utf-8">\
<meta name="viewport" content="width=device-width, initial-scale=1.0, shrink-to-fit=no">\
@BOOTSTRAP_STYLESHEET_LINK@\
<title>@CMAKE_PROJECT_NAME@ manual</title>\
</head>\
<body style="background-color: @BACKGROUND_COLOR@;color: @TEXT_COLOR@;margin: @SECTION_INDENTATION@ 0px;">\
<div class="container" style="/*background: var(--bs-gradient);*/">\
<div class="row">

# transform the titles

/\.TH/{
   # get rid of the .TH
   s/^\.TH[[:blank:]]*//
   # parenthesize the manual section number
   s/\([[:blank:]]*\)\([[:digit:]]\+\)/\ (\2)\n/
   # delimit the manual title marked up for left and right columns
   s/\(.*\)\n/<div\ class="col-5 col-sm-5 col-md-4"\ style="height:\ 32px;"><h5\ style="color:\ @TITLE_COLOR@;">\1<\/h5><\/div>\n<div\ class="col-5 col-sm-5 col-md-4"><h5\ class="text-end"\ style="color:\ @TITLE_COLOR@;">\1<\/h5><\/div>\n/
   # unquote and delimit the date
   s/\(.*\n\)\(.*\n\)[[:blank:]]*\("\(\([[:alnum:]]\|[[:blank:]]\|,\)*\)"\)/\1\2<div\ class="col-2 col-sm-2 col-md-4"><h5\ class="text-center"\ style="color:\ @TITLE_COLOR@;">\4<\/h5><\/div>\n/
   # unquote and delimit the version
   s/\(.*\n\)\(.*\n\)\(.*\n\)[[:blank:]]*\("\(\([[:alnum:]]\|[[:blank:]]\|\.\)*\)"\)/\1\2\3<div\ class="col-5 col-sm-5 col-md-4"><h5\ style="color:\ @TITLE_COLOR@;">\5<\/h5><\/div>\n/
   # unquote and delimit the manual page entry name
   s/\(.*\n\)\(.*\n\)\(.*\n\)\(.*\n\)[[:blank:]]*\("\(\([[:alnum:]]\|[[:blank:]]\)*\)"\)/\1\2\3\4<div\ class="col-2 col-sm-2 col-md-4"><h5\ class="text-center"\ style="color:\ @TITLE_COLOR@;">\6<\/h5><\/div>/
   # save all the titles for the footer
   h
   # select only the titles to be printed in the header
   s/\(.*\)\n\(.*\)\n\(.*\)\n\(.*\)\n\(.*\)/\1\n\5\n\2\n<\/div><div><p><\/p><\/div>/
   # print the titles
   p
   # delete the next line if it's empty
   n
   /./!d}

# interpret antibreaks
:a
/\\c$/{N; s/\\c\n//; ta}

# print the intermediate lines
p

# if it's the last line, print the footer

${x; s/\(.*\)\n\(.*\)\n\(.*\)\n\(.*\)\n\(.*\)/<div class="row" style="height: 32px;padding: 20px 0px;">\4\n\3\n\2<\/div>/; p}
$a\
</body>
