#!@SED@ -nf

# Consecutively number the reserved error codes in a header file
# starting from the defined minimum, and leave the final count in a
# comment on the last line. This script isn't meant to be invoked
# manually but to be called by the renumber script.

# find the base error code number, which must precede others in the input file
/@PROJECT_NAMESPACER@_MIN_ERR/{
   h
   s/\(.*\)[[:blank:]]\([0-9]\+$\)/\2/
   x}

# detect a reserved error code declaration
/^\#define[[:blank:]]\+@PROJECT_NAMESPACER@_[A-Z]\+[[:blank:]]\+(-[0-9]\+)[[:blank:]]*$/{
   # pull the count out of the hold space
   G
   # insert the count and remove delimiters
   s/\(.*\)\((-[0-9]\+\)\(.*\n\)\(.*\)/\1(-\4)/
   x
   # increment the count in the hold space by Bruno Haible's method
   :c
   s/9\(_*\)$/_\1/
   tc
   s/^\(_*\)$/1\1/; td
   s/8\(_*\)$/9\1/; td
   s/7\(_*\)$/8\1/; td
   s/6\(_*\)$/7\1/; td
   s/5\(_*\)$/6\1/; td
   s/4\(_*\)$/5\1/; td
   s/3\(_*\)$/4\1/; td
   s/2\(_*\)$/3\1/; td
   s/1\(_*\)$/2\1/; td
   s/0\(_*\)$/1\1/; td
   :d
   y/_/0/
   x
}

# print the line including any renumbered codes
p

# print the final count as a comment on the last line
${x; s/^/\/\//; p}
